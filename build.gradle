plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.8.0'
    id "maven-publish"
    id 'java-library'
    id 'signing'
    id 'org.jetbrains.dokka' version '0.10.1'
}

group 'com.github.alexburlton'
version '3.0.0'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.github.romankh3:image-comparison:4.4.0'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.8.0"
    implementation 'io.kotest:kotest-assertions-core:5.5.4'
    implementation "org.junit.jupiter:junit-jupiter-api:5.9.2"

    testImplementation "io.mockk:mockk:1.13.4"
    testImplementation 'org.mockito:mockito-core:3.3.3'
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.9.2"
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

dokka {
    outputFormat = 'html'
    outputDirectory = "$buildDir/dokka"
}

test {
    useJUnitPlatform()

    testLogging {
        events "failed"
        exceptionFormat "short"
        info.events = ["failed", "skipped"]
    }
}

task javadocJar(type: Jar) {
    archiveClassifier.set("javadoc")
    from javadoc
}

task sourcesJar(type: Jar) {
    archiveClassifier.set("sources")
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = 'com.github.alexburlton'
            artifactId = 'swing-test'
            version = '3.0.0'
            from components.java

            pom {
                name = 'swing-test'
                description = 'Kotlin swing testing library'
                url = 'https://github.com/alyssaburlton/swing-test'

                licenses {
                    license {
                        name = 'MIT'
                        url = "https://opensource.org/licenses/mit-license.php"
                    }
                }

                developers {
                    developer {
                        id = 'alyssaburlton'
                        name = 'Alyssa Burlton'
                    }
                }

                scm {
                    url = "https://github.com/alyssaburlton/swing-test"
                }
            }
        }
    }

    repositories {
        maven {
            name = "OSSRH"
            url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username = project.ossrhUsername
                password = project.ossrhPassword
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}
